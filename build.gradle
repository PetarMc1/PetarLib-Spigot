plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.3.1'
}

group = 'com.petarmc.petarlib'
version = '0.4.1'

def publishExcludes = [
        'com/petarmc/petarlib/PetarLib*',
        'com/petarmc/petarlib/Config*',
        'com/petarmc/petarlib/commands/**',
        'com/petarmc/petarlib/expansion/**'
]

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
}

configurations {
    bukkitImplementation
}

dependencies {
    implementation 'org.jetbrains:annotations:15.0'

    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"
    compileOnly 'me.clip:placeholderapi:2.12.2'
    compileOnly 'net.kyori:adventure-api:4.26.1'
    compileOnly 'net.kyori:adventure-text-minimessage:4.26.1'
    compileOnly 'net.kyori:adventure-platform-bukkit:4.4.1'
    bukkitImplementation 'net.kyori:adventure-platform-bukkit:4.4.1'
    bukkitImplementation 'net.kyori:adventure-text-minimessage:4.26.1'
}

tasks {
    runServer {
        minecraftVersion("1.21.11")
    }
}

def targetJavaVersion = 21
java {
    withSourcesJar()
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

def buildNum = project.findProperty('buildNumber')?.toString();
if (buildNum) {
    project.version = "${project.version}-${buildNum}"
}


def repoUrl = project.version.toString().toUpperCase().contains("SNAPSHOT") ?
        'https://repo.petarmc.com/repository/snapshots/' :
        'https://repo.petarmc.com/repository/releases/'


javadoc{
    destinationDir = file("$rootDir/javadoc")
    source = sourceSets.main.allJava.matching {
        exclude publishExcludes
    }
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.windowTitle = 'PetarLib Javadocs'
    options.docTitle = 'PetarLib Documentation'
}

tasks.register('mavenJar', Jar) {
    from sourceSets.main.output
    exclude publishExcludes
    destinationDirectory = layout.buildDirectory.dir('libs/maven')
}

tasks.register('mavenSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource.matching {
        exclude publishExcludes
    }
    destinationDirectory = layout.buildDirectory.dir('libs/maven')
}

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}


shadowJar {
    archiveClassifier.set('bukkit')
    mergeServiceFiles()
    configurations = [project.configurations.bukkitImplementation, project.configurations.runtimeClasspath]
}

tasks.build {
    dependsOn tasks.named('jar')
    dependsOn tasks.named('shadowJar')
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            groupId = project.maven_group
            artifactId = 'petarlib-bukkit'
            version = project.version
            artifact tasks.named('mavenJar')
            artifact tasks.named('mavenSourcesJar')
        }
    }

    repositories {
        maven {
            url = uri(repoUrl)
            credentials {
                username = project.findProperty('repoUsername') ?: ""
                password = project.findProperty('repoPassword') ?: ""
            }
        }
    }
}