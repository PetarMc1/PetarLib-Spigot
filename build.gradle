plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'com.petarmc.petarlib'
version = project.findProperty("version")

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}
dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.14-R0.1-SNAPSHOT"
//    implementation 'net.kyori:adventure-platform-bukkit:4.4.1'
}

tasks {
    runServer {
        minecraftVersion("1.21.11")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

def buildNum = project.findProperty('buildNumber')?.toString()
if (buildNum) {
    tasks.named('jar', Jar) {
        archiveFileName.set("PetarLibSpigot-v${version}-${buildNum}.jar")
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            groupId = project.maven_group
            artifactId =  'petarlib-spigot'
            version = project.findProperty('version')
            from components.java
        }
    }

    repositories {
        maven {
            url = uri('https://repo.petarmc.com/repository/releases/')
            credentials {
                username = project.findProperty('repoUsername') ?: ""
                password = project.findProperty('repoPassword') ?: ""
            }
        }
    }
}

javadoc{
    destinationDir = file("$rootDir/javadoc")
    source = sourceSets.main.allJava.matching {
        exclude 'com/petarmc/petarlib/PetarLib.java'
        exclude 'com/petarmc/petarlib/commands/*'
    }
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.windowTitle = 'PetarLib Javadocs'
    options.docTitle = 'PetarLib Documentation'
}

shadowJar{
    if (project.findProperty('buildNumber')) {
        archiveFileName.set("PetarLibSpigot-v${version}-${project.findProperty('buildNumber')}.jar")
    } else {
        archiveFileName.set("PetarLibSpigot-v${version}.jar")
    }
    archiveClassifier.set('')
//    mergeServiceFiles()
}
